<style>
  h2 {
    counter-increment:count1;
    counter-reset:count2;
  }
  h2:before {
    content:"2." counter(count1) " ";
  }

  h3 {
    counter-increment:count2;
  }
  h3:before {
    content:"2." counter(count1) "." counter(count2) " ";
  }
</style>

<div class="chapter">
<div class="chapter_description">
  <div class="chapter_description_left">
    <img width="100" src="https://cdn-ak.f.st-hatena.com/images/fotolife/p/perlcodesample/20180820/20180820075726.png">  </div>
  <div class="chapter_description_right">
    日本語CSVファイル処理をサンプルを使って解説していくよ。Perlでテキスト処理を行うための、print文、文字列、変数、配列、ハッシュ、条件分岐、繰り返しといった基本的な文法も解説するね。  </div>
</div>

<h2>Perlの基本文法の解説について</h2>

<h3>基本的な文法をプログラムで実行しよう</h3>

この章では、できるだけ短く、Perlの文法について解説します。print文、文字列、配列、ハッシュ、条件分岐、繰り返しといった基本的な文法をプログラムを実行できることを目標に学びましょう。文字列、配列、ハッシュ、if文、for文などの詳細については、それぞれの各章で、詳しく解説します。

<h3>日本語のCSVファイルをファイル入出力するプログラムのサンプル</h3>

次のプログラムを理解して実行できるようになることを目標にします。

<h4>入力データ</h4>

入力はファイルに書かれたCSVデータです。CSVとは、カンマで区切られたデータのことです。文字コードはUTF-8で保存します。入力ファイル名は「tut_input.csv」とします。

<pre>
ID,書名,著者名,価格
1,Perlテキスト処理プログラミング入門,木本裕紀,2900
2,Perl Web開発入門,木本裕紀,2000
3,データベース入門,田中太郎,1900
4,正規表現テクニック,田中太郎,3000
</pre>

<h4>出力データ</h4>

田中さんの書籍だけが出力されたCSVデータをファイル「tut_output.csv」に出力します。書名に入っている「入門」という用語を「基礎」に変更します。

<pre>
3,データベース基礎,田中次郎,1900
4,正規表現テクニック,田中次郎,3000
</pre>

Perlを使った検索と置換を体験できるサンプルです。

<h4>Perlプログラム</h4>

Perlのプログラムです。この章で学んだことで、理解できる内容にしています。実務でも、そのまま利用できます。ファイル名は「tut_example1.pl」です。

<pre>
use strict;
use warnings;
use utf8;
use Encode 'encode', 'decode';

# 引数に指定したファイルから行を一行ずつ読み込む
while (my $line = <>) {
  
  # PerlのUTF-8を内部文字列へデコード
  $line = decode('UTF-8', $line);
  
  # 改行を削除
  chomp $line;
  
  # カンマで分割して配列へ
  my @items = split(/,/, $line);
  
  # 本の情報をハッシュへ保存
  my %book;
  $book{id} = $items[0];
  $book{name} = $items[1];
  $book{author} = $items[2];
  $book{price} = $items[3];
  
  # 著者名に田中が含まれていたら
  if ($book{author} =~ /田中/) {
    
    # 書名の入門を基礎に置換
    $book{name} =~ s/入門/基礎/;
    
    # 出力するための配列を作成
    my @output_items = ($book{id}, $book{name}, $book{author}, $book{price});
    
    # 出力行を作成
    my $output_line = join(',', @output_items);
    
    # Perlの内部文字列をUTF-8にエンコードして出力
    print encode('UTF-8', $output_line) . "\n";
  }
}
</pre>

実行してみましょう。

<pre>
perl tut_example1.pl tut_input.csv > tut_output.csv
</pre>

うまくプログラムが実行できましたか? 慣れてくると、何も見ないで、手書きで、5分程度で、このプログラミングが書けるようになります。

では、このプログラムを理解するための、必要最小限な解説を行っていきます。

<h2>画面に文字列を表示する</h2>

まず最初に、プログラミングのABCである「Hello World!」を画面に表示してみましょう。

画面に文字を表示するには、print文を使います。「Hello World」という文字列を表現するにはダブルクォート「"」で文字「Hello World」を囲みます。文字の並びをプログラミングでは、文字列といいます。Perlプログラムの文の最後はセミコロン「;」で終わります。

<pre>
print "Hello World";
</pre>

これを「hello.pl」という名前で保存してください。

プログラミングを実行するには、perlコマンドを使用します。

<pre>
perl hello.pl
</pre>

画面に以下のように表示されます。

<pre>
Hello World
</pre>

「Hello World!」の後ろに改行がされずに、後ろに何か文字が続いている場合があるかもしれません。改行については、少し先で解説します。

プログラムでは、文字列を画面に表示して、プログラムの実行結果を確認します。この作業をプログラムが正しく動くまで繰り返します。

<h2>Perlの文法だけをチェックする</h2>

「-c」オプションを使うと、Perlの文法チェックだけを行うことができます。プログラムは実行されません。Perlの文法チェックだけを先に行いたいときに便利です。

<pre>
perl -c hello.pl
</pre>

以下のように出力されます。

<pre>
hello.pl syntax OK
</pre>

内部的には、PerlはいったんASTと呼ばれる抽象構文木という構造にコンパイルされて、実行されます。

<h2>改行する</h2>

Perlプログラムで改行をするには「/n」を使います。「/」はバックスラッシュですが、Windows環境であれば円マーク「&yen;」になっています。

<pre>
print "Hello World\n";
</pre>

このようなバックスラッシュで始まる特殊な文字をエスケープシーケンスといいます。ダブルクォート「"」で囲まれた文字列の中で「\n」と書くと改行を意味し、これはエスケープシーケンスのひとつです。

これを「hello_newline.pl」という名前で保存してください。

プログラミングを実行するには、perlコマンドを使用します。

<pre>
perl hello_newline.pl
</pre>

画面に以下のように表示されます。改行がされています。

<pre>
Hello World
</pre>

<h2>コメント</h2>

Perlのコメントは「#」で始まります。コメントは、プログラムとして意味をもちません。

<pre>
# コメント
</pre>

プログラムには、他の人が読むことを想定して、意図を表現した、わかりやすく、短いコメントをつけるのがお勧めです。

<pre>
# foo.comのアクセス回数をカウントする
my $accesss_count = 0;
if ($line =~ /foo\.com/) {
  $access_count++;
}
</pre>

<h2>複数行コメント</h2>

Perlには複数行コメントの文法が存在しませんが、ドキュメントを記述するPODと呼ばれる記法を使うと、複数行コメントが書けます。

<pre>
=pod

コメントにしたい文章
コメントにしたい文章

=cut
</pre>

一時的に、プログラムの一部をコメントしたい場合に便利です。

<h2>Perlのお約束</h2>

Perlのお約束です。Perlプログラムの先頭には、文法チェックを厳格にし、警告を表示するように、以下の二行を追加します。

<pre>
use strict;
use warnings;
</pre>

「use strict」の代表的な効果としては、変数宣言がされていない場合に、コンパイルが失敗します。

「use warnings」の代表的な効果としては、未定義値が意図しない場所で利用されていた場合に、警告が発生します。

<h2>変数宣言</h2>

変数の宣言について解説します。

<h3>変数宣言の方法</h3>

変数とは、値を保存しておく箱のようなものです。Perlの変数はmyを使って宣言します。変数名の先頭はドル「$」で始まります。

<pre>
# 変数宣言
my $num;
</pre>

「$」で始まる変数名は、正式にはスカラ変数と呼ばれます。スカラ変数には、一つの値を代入できます。Perlには、配列変数とハッシュ変数と呼ばれるものがありますが、それは後で解説します。

<h3>有効な変数名</h3>

変数名は、小文字、大文字、数字、アンダースコア「_」で構成することができます。

<pre>
# 有効な変数名
my $book_name;
my $BOOK;
my $author2;
</pre>

<h3>推奨される変数名</h3>

Perlの慣習として、推奨される変数名は、小文字とアンダスコア「_」で構成されるスネークケースです。慣習的に広く利用されているものと同じ書き方をしておくと、他の人が読みやすくなるというメリットがあります。
<pre>
# Perlの慣習として推奨される変数名
my $num;
my $book_name;
my $access_count;

# Perlの慣習として推奨されない変数名
my $Num;
my $BookName;
my $AccessCount;
</pre>

変数名の付け方のコツは、他の変数と区別ができる、意味のある名前を付けることです。変数がプログラムを解説してくれて、簡単なコメントがあれば、プログラムが読めるというのが、グッドです。

<h3>変数の初期化を宣言と同時に行う</h3>

変数の初期化を宣言と同時に行うことができます。

<pre>
# 変数の宣言と初期化を同時に行う
my $num = 1;
</pre>

<h2>数値</h2>

数値の表現について解説します。

<h3>数値リテラル</h3>

数値は、数値リテラルを使って表現します。整数リテラルで整数を、浮動小数点リテラルで、浮動小数点を表現できます。

<pre>
# 整数リテラル
1;
100_000_000;

# 浮動小数点リテラル
1.234;
</pre>

数値リテラルは、プログラムのソースコード上での数値の表現のことで、値が変更されない定数です。整数リテラルでは、アンダースコアを桁区切りとして利用することもできます。

数値リテラルは、スカラ変数に代入できます。

<pre>
# 数値リテラルを変数に代入
my $num = 1;
my $num = 1.234;
my $num = 100_000_000;
</pre>

Perlの数値は、プログラムの表現上は、64bit浮動小数点として扱われると考えてください。整数の場合は内部的には最適化のために整数として保持されるという場合があるのですが、プログラムからみた場合は、Perlの数値は、64bit浮動小数点と考えてください。

<h3>数値の計算</h3>

Perlで数値を使って計算してみましょう。足し算「+」、引き算「-」、掛け算「*」、割り算「/」を行ってみましょう。

<pre>
# 足し算 結果は2
my $add = 1 + 1;

# 割り算 結果は2
my $subtract = 3 - 1;

# 掛け算 結果は6
my $multiply = 3 * 2;

# 割り算 結果は0.5
my $division = 1 / 2;
</pre>

余りと商の求め方を紹介しておきます。商を求めるには普通の割り算を行った後にint関数で整数部を取り出します。

<pre>
# 商 結果は2
my $div = int(5/2);

# 余り 結果は1
my $mod = 5 % 2;
<pre>

<h3>演算子の優先順位</h3>

演算子の優先順位は、掛け算、割り算が、足し算、引き算に優先します。

<pre>
# 掛け算が優先 結果は17
my $num = 2 + 3 * 5;
</pre>

優先順位を変えたい場合や、はっきりさせたい場合は、丸かっこを使います。

<pre>
# 丸かっこが優先 結果は25
my $num = (2 + 3) * 5;
</pre>

<h3>数値計算の結果を変数に保存する</h3>

計算結果を変数に保存して、計算を進めていくことができます。

<pre>
my $num1 = 1 + 2 * 3;
my $num2 = 4 * 5;
my $num3 = $num1 + $num2;

# 結果は27
print "$num3\n";
</pre>

<h2>文字列</h2>

文字列について解説します。

<h3>文字列の表現</h3>

文字列は、文字の並びを表現します。文字列は、シングルクォート「'」で囲む方法と、ダブルクォート「"」で囲む方法の二種類の表現方法があります。

<pre>
# シングルクォートで囲った文字列
'Hello';

# ダブルクォートで囲った文字列
"Hello";
</pre>

<h3>シングルクォート文字列</h3>

シングルクォート文字列は、文字列を単なる文字列として、扱いたい場合に使います。

<h3>ダブルクォート文字列</h3>

ダブルクォートで囲った文字列の中では、エスケープシーケンスと変数展開を使うことができます。

エスケープシーケンスとは、改行やタブなどの特別な文字の表現です。

<pre>
# タブと改行をエスケープシーケンスで表現
my $message = "Foo\tBar\n";
</pre>

変数展開とは、変数の内容が文字列として展開される機能のことです。

<pre>
my $name = 'kimoto';

# 変数展開が行われる「I am kimoto」となる
my $message = "I am $name";
</pre>

<h3>文字列の連結</h3>

文字列を連結するには、文字列連結演算子「.」を使用します。

<pre>
# 「abcdef」になる
my $concat = 'abc' . 'def';
</pre>

<h3>文字列の詳細</h3>

文字列の詳細については「文字列」の章で詳しく解説します。

<h3>日本語処理</h\3>

文字列の解説で、日本語を使わないのは、Perlで日本語を扱う場合に、少し知識が必要だからです。日本語処理については、日本語処理を扱おうの章で詳しく解説します。

<h2>配列</h2>

配列について解説します。

<h3>配列とは</h3>

配列とは、複数の値を代入できる変数のことです。配列の先頭は@で始まります。

<pre>
# 配列の宣言
my @nums;
</pre>

配列は、リストを使って初期化できます。リストとは「(」と「)」で囲まれた、カンマで区切られた値の並びのことです。

<pre>
# 配列の宣言
my @nums = (5, 4, 3);
</pre>

配列には、文字列を代入することもできますし、数値と混在していてもかまいません。

<pre>
# 数値の配列
my @strs = ('foo', 'bar', 'baz');

# 数値と文字列が混在した配列
my @items = (1, 2, 'foo', 'bar');
</pre>

Perlは動的型を持つ言語なので、配列の要素の型を意識する必要はありません。

<h3>配列の要素の取得と設定</h3>

配列の要素の取得と設定を行うには、変数の後ろに「[インデックス]」を使います。インデックスは0から始まることに注意してください。

<pre>
# 配列の宣言 インデックスは0から数える
# 配列@numsのインデックス0の要素は5, インデックス1の要素は4、インデックス2の要素は3
my @nums = (5, 4, 3);

# 配列の要素の取得 3を取得
my $elem = $nums[2];

# 配列の要素の設定 4が1になる
$nums[1] = 5;
</pre>

<h3>配列の要素の画面への出力</h3>

配列は変数展開すると、要素が空白で区切られた値に変換されます。

<pre>
# 配列の宣言
my @nums = (5, 4, 3);

print "@$nums\n";
</pre>

出力結果は以下のようになります。

<pre>
5 4 3
</pre>

<h3>配列の詳細</h3>

配列の詳細は「配列」の章で扱います。

<h2>ハッシュ</h2>

Perlのハッシュについて簡単に説明します。

<h3>ハッシュとは</h3>

Perlのハッシュとは、キーと値の複数のペアを保存できる変数のことです。ソフトウェア用語では、連想配列と呼ばれているものです。

<h3>ハッシュの宣言</h3>

ハッシュの宣言です。ハッシュ名は「%」で始まります。

<pre>
# ハッシュの宣言
my %book;
</pre>

<h3>ハッシュへの代入</h3>

ハッシュにはキーと値のペアが入ったリストを代入することができます。ハッシュの宣言と同時に代入することもできます。

<pre>
# ハッシュへの代入
%book = (id => 1, name => 'Perl', price => '900');

my %book = (id => 1, name => 'Perl', price => '900');
</pre>

この例では、本という名前のハッシュを宣言して「ID」「書名」「価格」という情報を持たせています。

「=>」は「,」と同じ意味です。「=>」はファットカンマと呼ばれます。「=>」の左側の文字列は「a-zA-Z0-9_」で構成されている場合は、シングルクォートやダブルクォートを省略できます。

<pre>
# ハッシュへの代入
%book = (id => 1, name => 'Perl', price => '900')

# 上記は、以下と同じ意味
%book = ('id', 1, 'name', 'Perl', 'price', '900')
</pre>

Perlでは、このようなシンタックスシュガーと呼ばれる書き方を簡単し、意味を分かりやすくする記法がときどき登場するので、覚えておきましょう。

<h3>ハッシュの要素の参照と代入</h3>

ハッシュの要素の参照と代入です。ハッシュの要素の参照と代入は波かっこ「{キー名}」を使用します。

<pre>
# 要素の参照
$hash{'name'};
$hash{'price'};
</pre>

<pre>
# 要素の代入
$hash{'name'} = 'Database';
$hash{'price'} = 2000;
</pre>

ハッシュのキーは文字列ですが「a-zA-Z0-9_」で構成されている場合は、シングルクォートやダブルクォートで囲む必要はありません。

<pre>
# 要素の参照を簡単に書く
$hash{name};
$hash{price};
</pre>

<pre>
# 要素の代入を簡単に書く
$hash{name} = 'Database';
$hash{price} = 2000;
</pre>

慣習としては、シングルクォートやダブルクォートで囲まない簡単に書く方が多く利用されています。

<h3>ハッシュの詳細</h3>

ハッシュの詳しい内容は、ハッシュの章で解説します。

<h2>条件分岐</h2>

プログラミングで条件を指定して、行う処理を選択することを、条件分岐といいます。条件分岐はif文を使って行うことができます。

<h3>if文を使った条件分岐のサンプル1 - 本の価格が1000円以下だったら</h3>

ここでは、もし本の価格が、1000円以下だったら「Low Price」と画面に出力するというサンプルを一つ書いてみます。

<pre>
use strict;
use warnings;

# 本の価格が1000円以下だったら「Low Price」と画面に出力
my $book_price = 900;

if ($book_price <= 900) {
  print "Low Price\n";
}
</pre>

ifの丸かっこ「()」の中に条件を書くことができます。「<=」は、○○以下を表す比較演算子です。条件が真になった場合は、ブロック「{}」の中の処理が実行されます。条件が真というのは、条件が満たされたという意味だと考えてください。Perlの真偽値については、if文を使った条件分岐の章で詳しく扱います。

<h3>if文を使った条件分岐のサンプル2 - 本の名前にPerlを含んでいたら</h3>

本の名前にPerlを含んでいたら「This is Perl Book.」と出力するif文を使ったサンプルです。正規表現と呼ばれる文法を使います。正規表現については、後の章で解説します。

<pre>
use strict;
use warnings;

# 本の価格が1000円以下だったら「Low Price」と画面に出力
my $book_name = 'Perl Tutorial';

if ($book_name =~ /Perl/) {
  print "This is Perl Book.\n";
}
</pre>

「=~」はパターンマッチ演算子と呼ばれ、正規表現にマッチした場合に真になります。

<h3>if文を使った条件分岐のサンプル3 - 著者名に木本を含んでいたら</h3>

著者名に木本を含んでいたら。「これは木本の著作」と出力するif文を使ったサンプルです。日本語を正しく扱うサンプルになっています。

Perlで日本語を扱う場合は、いくつか知っておかなければならい知識があるのですが、まずは、サンプルが実行できること優先しましょう。

まずは、Unix/Linux/Macの場合のサンプルです。

<pre>
use strict;
use warnings;
use utf8;
use Encode 'encode';

# 著者の名前に木本を含んでいたら「これは木本の著作です。」と画面に出力
my $author_name = '木本裕紀';

if ($author_name =~ /木本/) {
  my $message = "これは木本の著作です";
  print encode('UTF-8', $message) . "\n";
}
</pre>

次に、Windowsの場合のサンプルです。

<pre>
use strict;
use warnings;
use utf8;
use Encode 'encode';

# 著者の名前に木本を含んでいたら「これは木本の著作です。」と画面に出力
my $author_name = '木本裕紀';

if ($author_name =~ /木本/) {
  my $message = "これは木本の著作です";
  print encode('cp932', $message) . "\n";
}
</pre>

一つの注意として、ソースコードはUTF-8で保存する必要があります。UTF-8というのは、ひとつの文字の符号化形式です。メモ帳の場合は、名前を付けて保存を行う場合にUTF-8という項目があります。これは、比較的新しいWindowsの機能なので、Windows 7や8の場合は、UTF-8で保存できるテキストエディタを探しましょう。

「use utf8;」という行をソースコードに含めます。これは、ソースコードがUTF-8で書かれているという意味であり、Perlが日本語を内部的な文字列として正しく扱うようになります。

<pre>
# ソースコードがUTF-8で書かれていることを教える
use utf8;
</pre>

「use Encode 'encode';」でencode関数をインポートします。インポートとは、関数をそのソースコード内で利用できるようにするという意味です。encode関数は、画面に出力するときにPerlの内部的な文字列を、OSの文字コードに変換するために使用します。

<pre>
# OSの文字コードにエンコードするためのencode関数をインポート
use Encode 'encode';
</pre>

Unix/Linux/Macの場合は「UTF-8」です。これはOSの設定で変更できますが、UTF-8であることが大半であると思うので、UTF-8を想定します。

Windowsの場合は「cp932」です。これは、Shift_JISと似ています。

このようにPerlでは、プログラムの中ではPerlの内部的な文字列で扱い、外部に出力するときはエンコードするという方法をとります。

<h2>繰り返し</h2>

Perlの繰り返し処理の解説です。

<h3>for文とwhile文</h3>

Perlでは、繰り返しを行うときに、for文あるいはwhile文を使います。

<pre>
# for文 - C言語風for文
for (my $i = 0; $i < 10; $i++) {
  # 処理
  print "$i\n";
}

# for文 - イテレーター風for文
for my $i (0 .. 9) {
  # 処理
  print "$i\n";
}

# while文 - イテレーター風for文
my $i = 0;
while ($i < 10) {
  # 処理
  print "$i\n";
  
  $i++;
}
</pre>

ふたつのfor文、while文がありますが、三つの意味はすべて同じです。

<h3>while文を使った繰り返しのサンプル</h3>

while文の使い方のサンプルを解説してみます。

<pre>
use strict;
use warnings;

my $i = 0;
while ($i < 10) {
  
  print "$i\n";
  
  $i++;
}
</pre>

出力結果です。

<pre>
0
1
2
3
4
5
6
7
8
9
</pre>

while文の丸かっこ「()」の中には条件を書くことができます。while文の条件が真の場合にブロック「{}」の中の処理が実行されます。

今回の場合の「$iが10より小さい」という条件です。

最後の部分に「$i++」とあるのに注目してください。これはインクリメントと呼ばれ「$i」の値が、1加算されます。

<h3>繰り返しの詳細</h3>

繰り返しについては、繰り返し処理の章で詳しく解説します。

<h2>Perl標準関数</h2>

関数の使い方について学びましょう。関数は、特定の機能を提供する便利に使える道具のようなものだと考えてください。Perlには、標準関数と呼ばれる、デフォルトで使える関数が用意されています。

<h3>chomp関数 - 改行を削除する</h3>

chomp関数を紹介します。chomp関数は、改行を削除するための関数です。Perlプログラミングでは、ファイルから行を読み込んだときに、まず改行を削除するということを行います。

<pre>
# 改行を削除
chomp $line;
</pre>

Unix/Linux/Macの場合は改行コードはASCIIコードのLFです。Windowsの場合はデフォルトの改行コードはASCIIコードのCRLFです。chomp関数で削除する改行には、OSの改行コードが割り当てられています。

ASCIIコードとは、文字コードの一つで、キーボードで入力できる英数字、記号、タブや改行などの特殊な文字を表現できる8bitの文字コードというような感じでまずは理解しておいてください。

<h3>split関数 - 区切り文字を指定して配列に変換する</h3>

split関数を一つのサンプルとして紹介します。split関数は、指定した区切り文字で対象の文字列を分割し、配列に変換する関数です。

<pre>
# カンマ区切りのデータ
my $csv = '1,Perl,Kimoto,2900';

# カンマ区切りのデータを配列に変換
my @items = split(/,/, $csv);

# 配列の内容を出力 1 Perl Kimoto 2900
print "@items\n";
</pre>

split関数の第一引数には、区切り文字を正規表現「//」を指定できますが、今回は、単なるカンマ「,」です。第二引数には、対象の文字列を指定します。

戻り値は、配列です。(正確に書くと、戻り値はリストです。)

<h3>join関数 - 区切り文字で連結された文字列を取得</h3>

join関数を使うと、指定した区切り文字で、配列の各要素を連結した文字列を取得できます。

<pre>
# 配列
my @items = (1, 'Perl', 'Kimoto', 2900);

# join関数で、カンマで連結してCSVデータに変換
my $csv = join(',', @items);
<pre>

join関数の第一引数は、区切り文字です。第二引数は、配列です。(正確に書くと、第二引数以降は、リストです。)

<h2>日本語処理</h2>

Perlの日本語処理のお約束事についてだけ、ここに書きます。詳細については、日本語処理で扱います。

<h3>「use utf8;」を書いて、ソースコードはUTF-8で保存</h3>

「use utf8;」を書いて、ソースコードはUTF-8で保存します。

<pre>
use utf8;
</pre>

<h3>Encodeモジュールのencode関数とdecode関数をインポート</h3>

Encodeモジュールのencode関数とdecode関数をインポートしましょう。モジュールというのは、プログラムの機能をまとめた部品のようなものだと考えてください。Perlのモジュールは、一般的なソフトウェア用語でいうライブラリのことです。

<pre>
# Encodeモジュールのencode関数とdecode関数をインポート
use Encode 'encode', 'decode';
</pre>

<h3>ファイルから入力するときはdecode</h3>

ファイルから入力するときはdecode関数を使って、実際の文字コードから、Perlの内部的な文字列へデコードします。たとえば、読み込む対象のファイルがUTF-8で書かれていたら、UTF-8を指定、cp932で書かれていたらcp932を指定します。

<pre>
# 引数に指定したファイルから行を一行ずつ読み込む
while (my $line = <>) {
  
  # PerlのUTF-8を内部文字列へデコード
  $line = decode('UTF-8', $line);
</pre>

Perlでは、日本語はプログラムの中で、Perlの内部的な文字列に変換して、扱います。Perlの内部的な文字列の表現はlatin-1あるいはUTF-8ですが、忘れてください。とにかくdecode関数は、実際の文字コードをを、Perlが内部で扱う形式に変換する関数なのだということだけ、理解してください。

この内部文字列への変換が行われると、文字列関数や正規表現が、日本語の文字の正しい長さを認識し、日本語に対して完全に正しく動くようになります。

<h3>ファイルへ出力するときはencode</h3>

ファイルへ出力するときは、encode関数を使って、Perlの内部的な文字列から、実際の文字コードへエンコードします。ファイルへ出力する直前でエンコードします。encode関数は、decode関数と反対のことを行います。

<pre>
# Perlの内部文字列をUTF-8にエンコードして出力
print encode('UTF-8', $output_line) . "\n";
</pre>

Perlの日本語処理に対する混乱は、encode関数とdecode関数が、実際の文字コードから、実際の文字コードの変換を行っているように誤解してしまうことにあると思っています。これは、以前に利用されていたjcode.plやJcode.pmが、実際の文字コードを、実際の文字コードに変換するライブラリだからだと思います。

Perl 5.8以降の新しい文字列の扱いは、実際の文字コードを、Perlの内部的な文字列に変換するという仕組みであることを、しっかりと覚えておいてください。

<h2>ファイル入出力</h2>

ファイル入出力の最も簡単な方法を紹介します。

<h3>リダイレクトを使ったファイルへの出力</h3>

ファイルへ出力するには、リダイレクトと呼ばれる方法を使います。リダイレクトを使うと、画面への出力をファイルへの出力に切り替えることができます。リダイレクトは、Perlの機能ではなくコマンドプロンプトやシェルの機能です。

「>」がリダイレクトの記号です。リダイレクトを使って、画面への出力を「tut_output.csv」というファイルへの出力に切り替えています。

<pre>
perl tut_example1.pl tut_input.csv > tut_output.csv
</pre>

<h3>ダイヤモンド演算子を使ったファイルからの入力</h3>

ダイヤモンド演算子「<>」を使うと、コマンドライン引数で指定したファイルから、1行づつ読み込むことができます。ダイヤモンド演算子は通称です。本当の名前は「行入力演算子」です。

<pre>
# 引数に指定したファイルから行を一行ずつ読み込む
while (my $line = <>) {
  
}
</pre>

一行づつ読み込んで、ファイルの末尾に到達すると、ダイヤモンド演算子は偽を返し、whileループは終了します。while文の条件部に「my」を使った変数宣言を書いていますが、Perlの変数宣言が値を返す式なので、このように書けます。

コマンドライン引数とは、プログラム名の後ろに続く部分のことです。ここにファイル名を指定します。「tut_input.csv」が、コマンドライン引数です。

<pre>
perl tut_example1.pl tut_input.csv
</pre>

ダイヤモンド演算子を使うと「tut_input.csv」のファイルの内容を一行づつ読み込むことができます。

ファイル入出力については、ファイル入出力の章で詳しく解説します。

これで最初のサンプルを理解するためのすべての解説が終わりました。

次の章からは、Perlを自由に書けるようになるための、Perlの文法や正規表現などの理解と実践を進めていきましょう。
