<style>
  h2 {
    counter-increment:count1;
    counter-reset:count2;
  }
  h2:before {
    content:"4." counter(count1) " ";
  }

  h3 {
    counter-increment:count2;
  }
  h3:before {
    content:"4." counter(count1) "." counter(count2) " ";
  }
</style>

<div class="chapter">
<div class="chapter_description">
  <div class="chapter_description_left">
    <img width="100" src="https://cdn-ak.f.st-hatena.com/images/fotolife/p/perlcodesample/20180820/20180820075726.png">  </div>
  <div class="chapter_description_right">
    HTMLをサンプルに正規表現の検索・置換を実践してみましょう。
  </div>
</div>

<h2>正規表現の検索・置換をHTMLで実践</h2>

<h3>見出しのテキストを取得する</h3>

見出し「h1」のテキストを正規表現で取得してみましょう。

<pre>
<h1>Linuxサーバー管理入門</h1>
</pre>

正規表現で見出し「h1」のテキストである「UNIX/Linuxサーバー管理入門」を取得してみます。ソースコードはUTF-8で保存してください。

<pre>
use strict;
use warnings;
use utf8;
use Encode 'encode';

my $html = '<h1>Linuxサーバー管理入門</h1>';

my $head_text;
if ($html =~ /<h1>(.+)<\/h1>/) {
  $head_text = $1;
}
print encode('UTF-8', "$head_text\n");
</pre>

出力結果です。Windowsの場合はencode関数の「UTF-8」の部分を「cp932」にしてみてください。この後のサンプルも同じです。

<pre>
Linuxサーバー管理入門
</pre>

<h3>見出しのテキストを取得する - 最短マッチ</h3>

先ほどの見出しを取得するスクリプト、HTMLが次のようになっていたらどうなるんでしょうか?

<pre>
<h1>Linuxサーバー管理入門</h1>まちがえ閉じタグ</h1>
</pre>

先ほどのスクリプトを実行すると、以下の出力になります。

<pre>
Linuxサーバー管理入門</h1>まちがえ閉じタグ
</pre>

あれ、後ろの方の</h1>の直前までが取り出されてしまいました。

「*」や「+」などのPerlの正規表現の量指定子は、デフォルトで最も長い位置までマッチします。

これを、最も短い位置でマッチさせるようにするには、正規表現を以下のように書き変えます。量指定子の後ろに「?」をつけるのがポイントです。

<pre>
$html =~ /<h1>(.+?)<\/h1>
</pre>

では、プログラムを書き直してみましょう。

<pre>
use strict;
use warnings;
use utf8;
use Encode 'encode';

my $html = '<h1>Linuxサーバー管理入門</h1>まちがえ閉じタグ</h1>';

my $head_text;
if ($html =~ /<h1>(.+?)<\/h1>/) {
  $head_text = $1;
}
print encode('UTF-8', "$head_text\n");
</pre>

今度は正しく欲しい部分を取得することができました。

<pre>
Linuxサーバー管理入門
</pre>

<h3>HTMLからタグを削除する</h3>

正規表現で置換の構文を使って、HTMLからタグを削除してテキストの部分だけを取り出してみましょう。

<pre>
今日は<b>プログラミング</b>を学んで充実した一日。今度は<span style="font-size:18px">Webシステム開発構築</span>を行いたいです。
</pre>

「<b>」「</b>」「<span style="font-size:18px">」「</span>」を取り除くことができる正規表現を考えます。

プログラムを書きます。

<pre>
use strict;
use warnings;
use utf8;
use Encode 'encode';

my $html = '今日は<b>プログラミング</b>を学んで充実した一日。今度は<span style="font-size:18px">Webシステム開発構築</span>を行いたいです。';

# 元のHTMLを残しておくために文字列を置換前にコピーする
my $text = $html;

# <と>で囲まれている部分をすべて削除する
$text =~ s/<.+?>//g;

print encode('UTF-8', "$text\n");
</pre>

正規表現の解説です。「<」がきて、改行以外のなんでもよい文字が1文字以上で最短マッチ「.+?」、「>」がくる。gオプションは、すべて置換を意味しています。

出力結果です。タグが取り除かれています。

<pre>
今日はプログラミングを学んで充実した一日。今度はWebシステム開発構築を行いたいです。
</pre>

<h3>前後の空白文字を取り除く</h3>

前後の空白文字を取り除いてみましょう。目には見えませんが、全角のスペースと半角のスペースが、前後に複数含まれています。

<pre>
　   Linuxサーバー管理入門　  
</pre>

「先頭から複数の空白を取り除く」「末尾から複数の空白を取り除く」という正規表現を書きます。

<pre>
# 前後の空白文字を取り除く
$text =~ s/^\s+//;
$text =~ s/\s+$//;
</pre>

<b>trim.pl</b>

<pre>
use strict;
use warnings;
use utf8;
use Encode 'encode';

my $text = '　   Linuxサーバー管理入門　  ';

# 先頭から複数の空白を取り除く
$text =~ s/^\s+//;

# 末尾から複数の空白を取り除く
$text =~ s/\s+$//;

print encode('UTF-8', "$text\n");
</pre>

出力結果です。

<pre>
Linuxサーバー管理入門
</pre>

「\s」という正規表現は、Unicodeで空白文字として定義されている文字を意味します。ですので、ASCIIコードの空白文字である、半角スペース、タブ、改行に加え、Unicodeで空白文字として定義されている、日本語の全角スペースも対象になります。

もしASCIIコードで空白とみなされる空白だけを意味したいのであれば、次のように書きます。特殊な文字クラスである「\p{PosixSpace}」を使って表現できます。

<b>trim_ascii.pl</b>

<pre>
# 先頭から複数の空白を取り除く
$text =~ s/^\p{PosixSpace}+//;

# 末尾から複数の空白を取り除く
$text =~ s/\p{PosixSpace}$//;
</pre>

Perl 5.14以降であれば、ASCIIコードのみを意味する「a」オプションを使って以下のように簡単に書けます。

<pre>
# 先頭から複数の空白を取り除く
$text =~ s/^\s+//a;

# 末尾から複数の空白を取り除く
$text =~ s/\s+$//a;
</pre>

<h3>pタグで作られた複数行の段落から本文だけを取り出す</h3>

複数行のpタグで作られた段落から本文だけを取り出すということをやってみましょう。先頭、末尾の空白は削除して、間の複数の空白はひとつの空白に置換してみましょう。

<pre>
  <p>
    今日は、毎週土曜日更新のPerlプログラミングちゃんねるを見ています。
    
    ひげで、坊主のオジサンが雑にしゃべっているのが気になり、講義が頭の中に入ってきません。
  </p>
</pre>

正規表現を書いてみましょう。ポイントは「.」は改行以外のすべての文字にマッチするのですが、今回の場合は、改行を含めたすべての文字をマッチされる必要があります。このような場合は、「.」をすべての文字にマッチされるようにする「s」オプション(シングルラインオプション)を使用します。最短マッチでマッチさせます。

<pre>
my $text;
if ($html =~ /<p>(.*?)<\/p>/s;) {
  $text = $1;
  
  # 先頭の空白文字を削除
  $text =~ s/^\s+//;
  
  # 末尾の空白文字を削除
  $text =~ s/\s+$//;
  
  # 複数の空白文字を一つの空白へ
  $text =~ s/\s+/ /;
}
</pre>

サンプルコード全体です。

<pre>
use strict;
use warnings;
use utf8;
use Encode 'encode';

# ヒアドキュメントで複数行を表現
my $html = <<'EOS';
  <p>
    今日は、毎週土曜日更新のPerlプログラミングちゃんねるを見ています。
    
    ひげで、坊主のオジサンが雑にしゃべっているのが気になり、講義が頭の中に入ってきません。
  </p>
EOS

my $text;
if ($html =~ /<p>(.*?)<\/p>/s) {
  $text = $1;
  
  # 先頭の空白文字を削除
  $text =~ s/^\s+//;
  
  # 末尾の空白文字を削除
  $text =~ s/\s+$//;
  
  # 複数の空白文字を一つの空白へ
  $text =~ s/\s+/ /;
}

print encode('UTF-8', "$text\n");
</pre>

出力結果です。

<pre>
今日は、毎週土曜日更新のPerlプログラミングちゃんねるを見ています。 ひげで、坊主のオジサンが雑にしゃべっているのが気になり、講義が頭の中に入ってきません。
</pre>

複数行の文字列を表現する場合に、ヒアドキュメントという機能がここでは使っています。

<pre>
# ヒアドキュメントで複数行を表現
my $html = <<'EOS';
  <p>
    今日は、毎週土曜日更新のPerlプログラミングちゃんねるを見ています。
    
    ひげで、坊主のオジサンが雑にしゃべっているのが気になり、講義が頭の中に入ってきません。
  </p>
EOS
</pre>

EOSの囲み文字を「"」に変えると、変数展開を行えるようになるので覚えておきましょう。

<pre>
# ヒアドキュメントで複数行を表現(変数展開あり)
my $language = 'Perl';
my $html = <<"EOS";
  <p>
    今日は、毎週土曜日更新の${language}プログラミングちゃんねるを見ています。
    
    ひげで、坊主のオジサンが雑にしゃべっているのが気になり、講義が頭の中に入ってきません。
  </p>
EOS
</pre>

<h2>HTMLファイルを処理する</h2>

HTMLファイルを読み込んで、処理してみましょう。上記、ヒアドキュメントで書いた内容を、入力ファイルに書いてUTF-8で保存してみます。

ファイルの内容を一度に読み込むには、行入力演算子のセパレターを表現する特殊変数「$/」を未定義にして、ファイル入力演算子で読み込みます。ファイルから読み込んだ内容は、decode関数でデコードして、Perlの内部文字列にするのを忘れないでください。

<pre>
# ファイル全部を読み込む
my $html;
{
  local $/ = undef;
  $html = <>;
  $html = decode('UTF-8', $html);
}
</pre>

入力ファイル「process_html_file_input.html」は、以下の内容で、UTF-8で保存してください。

<pre>
  <p>
    今日は、毎週土曜日更新のPerlプログラミングちゃんねるを見ています。
    
    ひげで、坊主のオジサンが雑にしゃべっているのが気になり、講義が頭の中に入ってきません。
  </p>
</pre>

HTMLファイルを読み込んで処理して、出力するサンプルです。

<pre>
use strict;
use warnings;
use utf8;
use Encode 'encode', 'decode';

# ファイル全部を読み込む
my $html;
{
  local $/ = undef;
  $html = <>;
  $html = decode('UTF-8', $html);
}

my $text;
if ($html =~ /<p>(.*?)<\/p>/s) {
  $text = $1;
  
  # 先頭の空白文字を削除
  $text =~ s/^\s+//;
  
  # 末尾の空白文字を削除
  $text =~ s/\s+$//;
  
  # 複数の空白文字を一つの空白へ
  $text =~ s/\s+/ /;
}

print encode('UTF-8', "$text\n");
</pre>

次のように実行してください。

<pre>
perl process_html_file.pl process_html_file_input.html > process_html_file_output.txt
</pre>

「process_html_file_output.txt」ファイルには、以下の内容が出力されます。

<pre>
今日は、毎週土曜日更新のPerlプログラミングちゃんねるを見ています。 ひげで、坊主のオジサンが雑にしゃべっているのが気になり、講義が頭の中に入ってきません。
</pre>

